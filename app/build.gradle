plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

def myCommitCount() {
    def cmd = 'git rev-list HEAD --count'
    return cmd.execute().text.trim().toInteger()
}

def myVersionName() {
    def tag = 'git describe --abbrev=0'.execute().text.trim()
    def commitId = 'git rev-parse --short HEAD'.execute().text.trim()
    return "${tag}_r${myCommitCount()}.${commitId}"
}


android {
    compileSdk 30

    buildFeatures {
        viewBinding true
    }

    defaultConfig {
        applicationId "com.bqliang.nfushortcuts"
        minSdkVersion 26
        targetSdkVersion 30
        versionCode myCommitCount()
        versionName myVersionName()
        buildConfigField("String", "CAIYUN_TOKEN", "\"${System.env.CAIYUN_TOKEN}\"")
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            manifestPlaceholders = [app_name:"@string/app_name_release"]
        }

        debug {
            applicationIdSuffix ".debug"
            manifestPlaceholders = [app_name:"@string/app_name_debug"]
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '11'
    }

    android.applicationVariants.all { variant ->
        def appName = "NFU Shortcut"
        def buildTime = LocalDateTime.now().toString().dropRight(10).replace("-", "").replace(":", "")
        variant.outputs.all { output ->
            outputFileName = "${appName}-${variant.buildType.name}-${myVersionName()}-${buildTime}.apk"
        }
    }
}

dependencies {
    // Welcome Pages
    implementation 'com.github.AppIntro:AppIntro:6.1.0'
    // 彩云 json 解析
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.1'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.5.1'
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.3.1"
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.google.code.gson:gson:2.8.8'
    implementation "com.squareup.retrofit2:retrofit:2.9.0"
    // Permission X
    implementation 'com.guolindev.permissionx:permissionx:1.5.1'
    // APP Center
    implementation "com.microsoft.appcenter:appcenter-distribute:4.2.0"
    implementation "com.microsoft.appcenter:appcenter-analytics:4.2.0"
    implementation "com.microsoft.appcenter:appcenter-crashes:4.2.0"
    // Glide
    implementation 'com.github.bumptech.glide:glide:4.12.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.12.0'
    // About Page
    implementation 'com.drakeet.about:about:2.4.1'
    // Lottie
    implementation 'com.airbnb.android:lottie:4.1.0'
    // Scan code
    implementation 'com.github.jenly1314:zxing-lite:2.1.1'

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}